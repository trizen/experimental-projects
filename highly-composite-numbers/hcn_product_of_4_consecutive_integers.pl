#!/usr/bin/perl

# Daniel "Trizen" È˜uteu
# Date: 02 July 2019
# https://github.com/trizen

# Find highly composite numbers H(n) that are the product of 4 consecutive integers.

# Known terms:
#   24, 120, 360, 840, 1680, 5040, 17297280

# See also:
#   https://oeis.org/A217056

use 5.020;
use strict;
use warnings;

use Math::GMPz;
use ntheory qw(:all);
use IO::Uncompress::Bunzip2;
use experimental qw(signatures declared_refs);

local $| = 1;
prime_precalc(1e7);

sub primorial_inflation ($n) {
    state %primorial;

    my $tmp  = Math::GMPz->new(1);
    my $prod = Math::GMPz->new(1);

    foreach my \@pp(factor_exp($n)) {
        my ($p, $e) = @pp;

        my $prim = $primorial{$p} //= do {
            my $z = Math::GMPz::Rmpz_init_nobless();
            Math::GMPz::Rmpz_primorial_ui($z, $p);
            $z;
        };

        if ($e > 1) {
            Math::GMPz::Rmpz_pow_ui($tmp, $prim, $e);
            Math::GMPz::Rmpz_mul($prod, $prod, $tmp);
        }
        else {
            Math::GMPz::Rmpz_mul($prod, $prod, $prim);
        }
    }

    return $prod;
}

# "HCN.bz2" was generated by Achim Flammenkamp, and is available at:
#   http://wwwhomes.uni-bielefeld.de/achim/HCN.bz2
# "HCN_primorial_deflated.txt.bz2" is a primorial deflation of each highly composite number H(n) from "HCN.bz2", such that A108951(A181815(H(n))) = H(n).
my $z = IO::Uncompress::Bunzip2->new("HCN_primorial_deflated.txt.bz2");

my $t = Math::GMPz->new(1);
my $u = Math::GMPz->new(1);

while (defined(my $line = $z->getline())) {

    chomp($line);
    my $hcn = primorial_inflation($line);

    Math::GMPz::Rmpz_root($t, $hcn, 4);

    Math::GMPz::Rmpz_set($u, $t);
    Math::GMPz::Rmpz_sub_ui($t, $t, 1);
    Math::GMPz::Rmpz_mul($u, $u, $t);
    Math::GMPz::Rmpz_add_ui($t, $t, 2);
    Math::GMPz::Rmpz_mul($u, $u, $t);
    Math::GMPz::Rmpz_add_ui($t, $t, 1);
    Math::GMPz::Rmpz_mul($u, $u, $t);

    if ($u == $hcn) {
        print $hcn, ", ";
    }
}
